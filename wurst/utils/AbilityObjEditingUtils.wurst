package AbilityObjEditingUtils

import ChannelAbilityPreset
import ErrorHandling
import HashMap

import TooltipsUtils
import CommonUtils

public function AbilityDefinition.setNameEnhance(string hotkey, string name)
    this.setName(name)
    this.setHotkeyNormalEnhance(hotkey)
    this.presetTooltipNormal(lvl -> makeToolTipNorm(hotkey, name))

public function createToggleAbility(int newId, int lvls) returns ChannelAbilityPreset
    return new ChannelAbilityPreset(newId, lvls, true)
        ..setDummyAbility()
        ..presetTargetTypes(Targettype.NONE)
        ..setCastingTime(1, 0)

public function AbilityDefinition.setHotkeyNormalEnhance(string hotkey)
    if not hotKeyMapping.has(hotkey)
        error("Hotkey button position mapping has no \"{0}\" element".format(hotkey))
        return
    this.setHotkeyNormal(hotkey)
    this.presetButtonPosNormal(hotKeyMapping.get(hotkey).x, hotKeyMapping.get(hotkey).y)
