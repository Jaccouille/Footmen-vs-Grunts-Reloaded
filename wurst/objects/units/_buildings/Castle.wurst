package Castle

import UnitObjEditing
import LinkedList
import ObjectIds
import UnitIds
import AbilityIds
import LocalObjectIDs
import IDListConstant

let ALLIANCE_RESEARCH = HUMAN_CASTLE_UPGD
                            ..addAll(NIGHT_ELF_CASTLE_UPGD)
                            .joinBy((int i) -> i.toRawCode(), ",")

let HORDE_RESEARCH = ORC_CASTLE_UPGD
                        ..addAll(UNDEAD_CASTLE_UPGD)
                        .joinBy((int i) -> i.toRawCode(), ",")


let KEEP_ABILITIES = commaList(AbilityIds.invulnerable)

function createKeepTemplate(int newId, int origId) returns BuildingDefinition
    return new BuildingDefinition(newId, origId)
    ..setNormalAbilities(KEEP_ABILITIES)
    ..setPlacementRequires("")
    ..setFoodProduced(0)
    ..setUnitsTrained("")
    ..setUpgradesUsed("")
    ..setPriority(11)

@compiletime function createFootmenKeep()
    createKeepTemplate(UNIT_ALLIANCE_CASTLE, UnitIds.castle)
    ..setGroundTexture("HMED")
    ..setName("Alliance Castle")
    ..setResearchesAvailable(ALLIANCE_RESEARCH)

@compiletime function createGruntsKeep()
    createKeepTemplate(UNIT_HORDE_CASTLE, UnitIds.fortress)
    ..setGroundTexture("OLAR")
    ..setName("Horde Castle")
    ..setNormalAbilities(KEEP_ABILITIES)
    ..setResearchesAvailable(HORDE_RESEARCH)
